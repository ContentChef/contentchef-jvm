apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

ext {
    publishVersion = '1.2.1'
    publishGroupId = 'io.contentchef'
}

task sourcesJar(type: Jar, group: "build") {
    archiveClassifier.set("sources")
    from(sourceSets.main.allSource)
}

artifacts {
    archives sourcesJar//, dokkaJar
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.bintrayUser : ''
    key = project.hasProperty('bintrayKey') ? project.bintrayKey : ''
    //noinspection GroovyAccessibility
    publications = ['Production']
    pkg {
        repo = 'maven'
        name = publishGroupId + ':' + publishArtifactId
        userOrg = project.hasProperty('bintrayOrganization') ? project.bintrayOrganization : ''
        desc = publishArtifactDescription
        websiteUrl = 'https://www.contentchef.io/'
        publicDownloadNumbers = true
        publish = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/ContentChef/contentchef-jvm'
        githubRepo = 'ContentChef/contentchef-jvm'
        githubReleaseNotesFile = 'README.md'
        version {
            name = publishVersion
            desc = publishVersion
            released = new Date()
            vcsTag = publishVersion
        }
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/contentchef/contentchef-jvm")
            credentials {
                username = findProperty("githubUsername")
                password = findProperty("githubToken")
            }
        }
    }
    publications {
        Production(MavenPublication) {
            from(components.java)
            artifact(tasks["sourcesJar"])
            groupId publishGroupId
            artifactId publishArtifactId
            version publishVersion
        }
    }
}